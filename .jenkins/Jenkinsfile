//file:noinspection GrUnresolvedAccess

def buildHash
def testdbUrl
def gemmaVersion
def baseCodeVersion
def dataDir
def deployRef
def deployServer
def cliRef
def cliWikiPageSuffix

void setBuildStatus(String context, String message, String state) {
    step([
        $class            : "GitHubCommitStatusSetter",
        reposSource       : [$class: "ManuallyEnteredRepositorySource", url: "https://github.com/PavlidisLab/Gemma"],
        contextSource     : [$class: "ManuallyEnteredCommitContextSource", context: context],
        errorHandlers     : [[$class: "ChangingBuildStatusErrorHandler", result: "UNSTABLE"]],
        statusResultSource: [$class: "ConditionalStatusResultSource", results: [[$class: "AnyBuildResult", message: message, state: state]]]
    ])
}

pipeline {
    agent any
    tools {
        maven 'Maven 3.6.3'
    }
    parameters {
        string(name: 'DATA_DIR', defaultValue: '/space/gemmaData', description: 'Directory where production data is stored.')
        string(name: 'STAGING_DATA_DIR', defaultValue: '/space/staginggemmaData', description: 'Directory where staging data is stored.')
        string(name: 'DEV_DATA_DIR', defaultValue: '/space/sandboxgemmaData', description: 'Directory where development data is stored.')
        string(name: 'MAVEN_SITES_DIR', defaultValue: '/space/web/maven-sites', description: 'Directory where Maven sites are deployed.')
        string(name: 'CLI_DIR_PREFIX', defaultValue: '/space/opt/gemma-cli', description: 'Prefix for Gemma CLI deployment directory.')
        string(name: 'DEPLOY_DIR_PREFIX', defaultValue: '/var/local/tomcat', description: 'Prefix for Gemma Web deployment directories.')
        // either hotfix or release, we only have one staging server & CLI symlink
        choice(name: 'STAGING_BRANCH', choices: ['hotfix', 'release'], description: 'Branch deployed to the staging server and CLI symlink.')
        // whether or not to force integration tests
        booleanParam(name: 'FORCE_INTEGRATION_TESTS', defaultValue: false, description: 'Force integration tests to be performed regardless of the branch being built.')
        booleanParam(name: 'FORCE_SLOW_TESTS', defaultValue: false, description: 'Force slow tests to be performed regardless of the branch being built.')
        booleanParam(name: 'FORCE_DEPLOY', defaultValue: false, description: 'Force deployment to be performed regardless of the branch being built.')
        booleanParam(name: 'FORCE_SONARQUBE', defaultValue: false, description: 'Force SonarQube analysis to be performed regardless of the branch being built.')
    }
    stages {
        stage('Checkout scm') {
            steps {
                checkout scm
                script {
                    gemmaVersion = sh script: 'mvn help:evaluate -Dexpression=project.version -q -DforceStdout', returnStdout: true
                    baseCodeVersion = sh script: 'mvn help:evaluate -Dartifact=baseCode:baseCode -Dexpression=project.version -q -DforceStdout', returnStdout: true
                    buildHash = env.GIT_COMMIT
                    def productionBuild = env.BRANCH_NAME == 'master'
                    def supportBuild = env.BRANCH_NAME.startsWith('support-')
                    def stagingBuild = env.BRANCH_NAME =~ '^' + params.STAGING_BRANCH + '-.*'
                    if (productionBuild) {
                        if (gemmaVersion.endsWith('-SNAPSHOT')) {
                            error("A production build must not have a -SNAPSHOT suffix.")
                        }
                        dataDir = params.DATA_DIR
                        deployServer = 'moe'
                        deployRef = 'gemma'
                        cliRef = 'production'
                        cliWikiPageSuffix = ' (generated)'
                    } else if (supportBuild) {
                        if (gemmaVersion.endsWith('-SNAPSHOT')) {
                            error("A support build must not have a -SNAPSHOT suffix.")
                        }
                    } else if (stagingBuild) {
                        if (!gemmaVersion.endsWith("-SNAPSHOT")) {
                            error("A staging build must have a -SNAPSHOT suffix.")
                        }
                        dataDir = params.STAGING_DATA_DIR
                        deployServer = 'chalmers'
                        deployRef = 'gemma-staging'
                        cliRef = 'staging'
                        cliWikiPageSuffix = ' (staging)'
                    } else {
                        if (!gemmaVersion.endsWith("-SNAPSHOT")) {
                            error("A development build must have a -SNAPSHOT suffix.")
                        }
                        dataDir = params.DEV_DATA_DIR
                        deployServer = 'chalmers'
                        deployRef = 'gemma'
                        cliRef = gemmaVersion
                        cliWikiPageSuffix = ' (development)'
                    }
                }
                sh 'mvn -B clean'
            }
        }
        stage('Build') {
            steps {
                setBuildStatus "Build", "Build #${env.BUILD_NUMBER} has started...", 'PENDING'
                sh 'mvn -B compile'
            }
            post {
                success {
                    setBuildStatus "Build", "Build #${env.BUILD_NUMBER} succeeded.", 'SUCCESS'
                }
                unsuccessful {
                    setBuildStatus "Build", "Build #${env.BUILD_NUMBER} failed.", 'FAILURE'
                }
            }
        }
        stage('Run quick unit tests') {
            steps {
                setBuildStatus "Tests", "Tests for build #${env.BUILD_NUMBER} have started...", 'PENDING'
                sh 'mvn -B test --fail-at-end -DexcludedGroups=SlowTest'
            }
            post {
                always {
                    junit testResults: '**/target/surefire-reports/*.xml'
                }
                success {
                    setBuildStatus "Tests", "Tests for build #${env.BUILD_NUMBER} have passed.", 'SUCCESS'
                }
                unsuccessful {
                    setBuildStatus "Tests", "Tests for build #${env.BUILD_NUMBER} failed.", 'FAILURE'
                }
            }
        }
        stage('Package') {
            steps {
                setBuildStatus "Package", "Package for build #${env.BUILD_NUMBER} have started...", 'PENDING'
                sh 'mvn -B package -DskipTests'
            }
            post {
                success {
                    archiveArtifacts artifacts: '**/target/*.jar, **/target/*.war', fingerprint: true
                    setBuildStatus "Package", "Package for build #${env.BUILD_NUMBER} have passed.", 'SUCCESS'
                }
                unsuccessful {
                    setBuildStatus "Package", "Package for build #${env.BUILD_NUMBER} failed.", 'FAILURE'
                }
            }
        }
        stage('Run integration tests and perform deployment in parallel') {
            when {
                anyOf {
                    branch 'master'
                    branch 'support-*'
                    branch 'hotfix-*'
                    branch 'release-*'
                    branch 'development'
                    expression { params.FORCE_INTEGRATION_TESTS }
                    expression { params.FORCE_SLOW_TESTS }
                    expression { params.FORCE_SONARQUBE }
                    expression { params.FORCE_DEPLOY }
                }
            }
            parallel {
                stage('Run slow unit tests') {
                    when {
                        anyOf {
                            // we can safely ignore integration tests on the master branch, those were run in hotfix/release
                            branch 'support-*'
                            branch 'hotfix-*'
                            branch 'release-*'
                            branch 'development'
                            expression { params.FORCE_SLOW_TESTS }
                        }
                    }
                    steps {
                        lock('gemma-slow-tests') {
                            setBuildStatus "Slow tests", "Slow tests #${env.BUILD_NUMBER} have started...", 'PENDING'
                            sh 'mvn -B test --fail-at-end -Dgroups=SlowTest -DskipWebpack'
                        }
                    }
                    post {
                        always {
                            junit testResults: '**/target/surefire-reports/*.xml'
                        }
                        success {
                            setBuildStatus "Slow tests", "Slow tests for build #${env.BUILD_NUMBER} have passed.", 'SUCCESS'
                        }
                        unsuccessful {
                            setBuildStatus "Slow tests", "Slow tests for build #${env.BUILD_NUMBER} failed.", 'FAILURE'
                        }
                    }
                }
                stage('Run integration tests') {
                    when {
                        anyOf {
                            // we can safely ignore integration tests on the master branch, those were run in hotfix/release
                            branch 'support-*'
                            branch 'hotfix-*'
                            branch 'release-*'
                            branch 'development'
                            expression { params.FORCE_INTEGRATION_TESTS }
                        }
                    }
                    steps {
                        setBuildStatus "Integration tests", "Integration tests #${env.BUILD_NUMBER} have started...", 'PENDING'
                        lock('database/gemdtest') {
                            sh 'mvn -B verify --fail-at-end -DskipUnitTests -DskipJavadoc -DskipWebpack'
                        }
                    }
                    post {
                        always {
                            junit testResults: '**/target/failsafe-reports/*.xml'
                        }
                        success {
                            setBuildStatus "Integration tests", "Integration tests for build #${env.BUILD_NUMBER} have passed.", 'SUCCESS'
                        }
                        unsuccessful {
                            setBuildStatus "Integration tests", "Integration tests for build #${env.BUILD_NUMBER} failed.", 'FAILURE'
                        }
                    }
                }
                stage('SonarQube Analysis') {
                    tools {
                        jdk 'Java 17'
                    }
                    when {
                        anyOf {
                            branch 'master'
                            branch 'support-*'
                            branch 'hotfix-*'
                            branch 'release-*'
                            branch 'development'
                            expression { params.FORCE_SONARQUBE }
                        }
                    }
                    steps {
                        lock('gemma-sonarqube-analysis') {
                            setBuildStatus "SonarQube Analysis", "SonarQube analysis for build #${env.BUILD_NUMBER} has started...", 'PENDING'
                            withSonarQubeEnv('UBC SonarQube') {
                                sh "mvn package -DskipTests -DskipJavadoc -DskipWebpack dependency-check:check org.sonarsource.scanner.maven:sonar-maven-plugin:sonar -Dsonar.projectKey=mslg -Dsonar.projectName='MSL - Gemma'"
                            }
                        }
                    }
                    post {
                        success {
                            setBuildStatus "SonarQube analysis", "SonarQube analysis for build #${env.BUILD_NUMBER} have passed.", 'SUCCESS'
                        }
                        failure {
                            setBuildStatus "SonarQube analysis", "SonarQube analysis for build #${env.BUILD_NUMBER} failed.", 'ERROR'
                        }
                        unstable {
                            setBuildStatus "SonarQube analysis", "SonarQube analysis for build #${env.BUILD_NUMBER} failed.", 'FAILURE'
                        }
                    }
                }
                stage('Deploy artifacts') {
                    when {
                        anyOf {
                            branch 'master'
                            branch 'hotfix-*'
                            branch 'release-*'
                            branch 'development'
                            expression { params.FORCE_DEPLOY }
                        }
                    }
                    steps {
                        sh 'mvn -B deploy -DskipTests -DskipJavadoc -DskipWebpack'
                    }
                }
                stage('Deploy Maven website') {
                    when {
                        anyOf {
                            branch 'master'
                            branch 'hotfix-*'
                            branch 'release-*'
                            branch 'development'
                            expression { params.FORCE_DEPLOY }
                        }
                    }
                    steps {
                        sh 'mvn -B site-deploy -DskipWebpack'
                        script {
                            if (dataDir != null) {
                                sh "ln -Tsf ${params.MAVEN_SITES_DIR}/gemma/gemma-${gemmaVersion} ${dataDir}/gemma-devsite"
                                sh "ln -Tsf ${params.MAVEN_SITES_DIR}/baseCode/baseCode-${baseCodeVersion} ${dataDir}/baseCode-site"
                            }
                        }
                    }
                }
                stage('Deploy Gemma Web') {
                    when {
                        anyOf {
                            branch 'master'
                            // we only support one staging server
                            branch params.STAGING_BRANCH + '-*'
                            branch 'development'
                            expression { params.FORCE_DEPLOY }
                        }
                    }
                    steps {
                        sh "env GEMMA_WEB_DEPLOY_SERVER=${deployServer} GEMMA_WEB_PREFIX=${params.DEPLOY_DIR_PREFIX} ./gemma-web/deploy.sh ${deployRef}"
                    }
                }
                stage('Deploy Gemma CLI') {
                    when {
                        anyOf {
                            branch 'master'
                            branch params.STAGING_BRANCH + '-*'
                            // never deploy the development CLI, it's too risky
                            // branch 'development'
                            expression { params.FORCE_DEPLOY }
                        }
                    }
                    steps {
                        sh "env GEMMA_CLI_PREFIX=${params.CLI_DIR_PREFIX} ./gemma-cli/deploy.sh ${cliRef}"
                        sh "env GEMMA_CLI_WIKI_PAGE_SUFFIX='${cliWikiPageSuffix}' ./gemma-cli/deploy-wiki.sh 'pavlab-wiki:Global/gemma/Gemma Landing Page/Gemma Curation/List of Gemma CLI Tools${cliWikiPageSuffix}'"
                    }
                }
            }
        }
    }
    post {
        fixed {
            slackSend color: 'good', message: "${env.JOB_NAME} - #${env.BUILD_NUMBER} Back to normal (<${env.BUILD_URL}|Open>)"
        }
        unsuccessful {
            slackSend color: 'danger', message: "${env.JOB_NAME} - #${env.BUILD_NUMBER} Failure (<${env.BUILD_URL}|Open>)"
        }
    }
}
